(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED " 5-Jan-2025 16:54:17" {DSK}<home>paolo>il>interpinkie>INTERPINKIE.;14 10084  

      :EDIT-BY "PA"

      :CHANGES-TO (VARS INTERPINKIECOMS)
                  (FNS BEGIN.FINGER.OUTPUT END.FINGER.OUTPUT HANDLE.FINGER.MENU)

      :PREVIOUS-DATE " 3-Jan-2025 20:48:14" {DSK}<home>paolo>il>interpinkie>INTERPINKIE.;13)


(PRETTYCOMPRINT INTERPINKIECOMS)

(RPAQQ INTERPINKIECOMS ((* A finger client. Requires the nc and tr Unix programs to be installed on 
                           the host operating system.)
                        (FNS BEGIN.FINGER.OUTPUT CREATE.FINGER.STREAM END.FINGER.OUTPUT 
                             HANDLE.FINGER.MENU PARSE.FINGER.QUERY PINKIE QUERY.FINGER.SERVER)))



(* A finger client. Requires the nc and tr Unix programs to be installed on the host operating system.
)

(DEFINEQ

(BEGIN.FINGER.OUTPUT
  [LAMBDA (DESTINATION USER HOST)                            (* ; "Edited  5-Jan-2025 16:49 by PA")
                                                             (* ; "Edited  1-Jan-2025 18:10 by PA")
                                                             (* ; "Edited 30-Dec-2024 10:50 by PA")

         (* Prepares DESTINATION for sending the response of a Finger query about USER at 
         HOST. For example, if DESTINATION is associated with a window the function sets 
         it to read-write, clear it, and sets a title that reflects the queried user and 
         host. In CLOS this function would be a :BEFORE method of QUERY.FINGER.SERVER.)

    [if (TYPENAMEP DESTINATION 'WINDOW)
        then (LET ((OUTSTREAM (TEXTSTREAM DESTINATION)))     (* Save query as input suggestion for 
                                                             the Finger menu command.)
                  (WINDOWPROP DESTINATION 'FINGERQUERY (CONCAT (OR USER "")
                                                              "@" HOST))
                  (TEXTPROP OUTSTREAM 'READONLY NIL)
                  (TEDIT.DELETE OUTSTREAM 1 (TEDIT.NCHARS OUTSTREAM))
                  (WINDOWPROP DESTINATION 'TITLE (CONCAT "Finger " (OR USER "")
                                                        "@" HOST]
    DESTINATION])

(CREATE.FINGER.STREAM
  [LAMBDA (NEWINP)                                           (* ; "Edited 30-Dec-2024 12:41 by PA")

         (* Creates if necessary an output destination suitable for printing a Finger 
         response to the primary output if NEWINP is NIL, to a new separate window 
         otherwise. Returns a stream if NEWINP is NIL, otherwise a window whose OUTSTREAM 
         property holds a stream to send the output to.)

    (if NEWINP
        then (LET* ((MENUW (MENUWINDOW (create MENU
                                              ITEMS _ '((Finger CMDFINGER "Query a Finger server")
                                                        (Exit CMDEXIT "Quit the program."))
                                              MENUFONT _ '(MODERN 12)
                                              TITLE _ "Commands"
                                              CENTERFLG _ T
                                              WHENSELECTEDFN _ (FUNCTION HANDLE.FINGER.MENU))
                                  T))
                    [TEDIT.PROCESS (TEDIT NIL NIL NIL '(FONT (TERMINAL 10]
                    (OUT (TEXTSTREAM TEDIT.PROCESS))
                    (MAINW (WFROMDS OUT)))
                   (ATTACHWINDOW MENUW MAINW 'RIGHT 'TOP)
                   (TEXTPROP OUT 'READONLY 'QUIET)
                   (WINDOWPROP MAINW 'TITLE "Finger")
                   MAINW)
      else (OUTPUT])

(END.FINGER.OUTPUT
  [LAMBDA (DESTINATION)                                      (* ; "Edited  5-Jan-2025 16:52 by PA")
                                                             (* ; "Edited 30-Dec-2024 10:51 by PA")

         (* Cleans up DESTINATION after printing the output of a Finger query.
         For example, if DESTINATION is a window the function marks it as not modified and 
         sets it read-only. In CLOS this function would be an :AFTER method of 
         QUERY.FINGER.SERVER.)

    [if (TYPENAMEP DESTINATION 'WINDOW)
        then (LET ((OUTSTREAM (TEXTSTREAM DESTINATION)))     (* Clear TEdit's prompt area.)
                  (TEDIT.PROMPTPRINT OUTSTREAM "" T)

         (* Mark buffer as not changed so the user is not prompted whether to save when 
         quitting the program.)

                  (TEDIT.STREAMCHANGEDP OUTSTREAM T)
                  (TEXTPROP OUTSTREAM 'READONLY 'QUIET]
    DESTINATION])

(HANDLE.FINGER.MENU
  [LAMBDA (ITEM MENU KEY)                                    (* ; "Edited  5-Jan-2025 16:44 by PA")
                                                             (* ; "Edited  1-Jan-2025 18:07 by PA")
                                                             (* ; "Edited 30-Dec-2024 16:54 by PA")
                                                             (* Handle Finger menu selection.)
    (LET [(MAINW (MAINWINDOW (WFROMMENU MENU]
         (if (MEMBER KEY '(MIDDLE RIGHT))
             then NIL
           else (SELECTQ (CADR ITEM)
                    (CMDFINGER (TEDIT.PROMPTPRINT (TEXTSTREAM MAINW)
                                      "" T)

         (* PROMPTFORWORD returns a string with no leading or trailing whitespace, so 
         PARSE.FINGER.QUERY needs not check for this.)

                               (LET* ([QUERY (L-CASE (PROMPTFORWORD 
                                                            "Finger query [username]@hostname:"
                                                            (WINDOWPROP MAINW 'FINGERQUERY)
                                                            NIL
                                                            (GETPROMPTWINDOW MAINW]
                                      (PARSED.QUERY (PARSE.FINGER.QUERY QUERY)))
                                     (if PARSED.QUERY
                                         then (LET ((USER (CAR PARSED.QUERY))
                                                    (HOST (CDR PARSED.QUERY)))
                                                   (BEGIN.FINGER.OUTPUT MAINW USER HOST)
                                                   (QUERY.FINGER.SERVER USER HOST (TEXTSTREAM MAINW))
                                                   (END.FINGER.OUTPUT MAINW))
                                       else (TEDIT.PROMPTPRINT (TEXTSTREAM MAINW)
                                                   "Invalid query." T))))
                    (CMDEXIT (TEDIT.QUIT (TEXTSTREAM MAINW)))
                    (T (PROMPTPRINT "Unknown menu item."])

(PARSE.FINGER.QUERY
  [LAMBDA (QUERY)                                            (* ; "Edited  1-Jan-2025 17:41 by PA")
                                                             (* ; "Edited 28-Dec-2024 19:00 by PA")

         (* Checks Finger QUERY and returns a cons
         (user . host) if valid, NIL otherwise. User is NIL if the username is missing 
         from QUERY. Assumes QUERY has no leading or trailing whitespace.)

    (LET ((@POS (OR (STRPOS "@" QUERY)
                    0)))                                     (* QUERY must have a minimum length 
                                                             and not contain more than one @ 
                                                             character.)
         (if [AND (GEQ (NCHARS QUERY)
                       10)
                  (NULL (STRPOS "@" QUERY (ADD1 @POS]
             then (CONS (LET [(USER (SUBSTRING QUERY 1 (SUB1 @POS]
                             (if (AND (GREATERP @POS 0)
                                      (GREATERP (NCHARS USER)
                                             0))
                                 then USER
                               else NIL))
                        (SUBSTRING QUERY (ADD1 @POS)))
           else NIL])

(PINKIE
  [LAMBDA (QUERY NEWINP)                                     (* ; "Edited  1-Jan-2025 17:46 by PA")
                                                             (* ; "Edited 30-Dec-2024 12:24 by PA")

         (* Performs Finger QUERY and print the response to the primary output if NEWINP 
         is NIL, to a new window otherwise.)

    (LET [(PARSED.QUERY (PARSE.FINGER.QUERY (L-CASE QUERY]
         (if PARSED.QUERY
             then (LET ((USER (CAR PARSED.QUERY))
                        (HOST (CDR PARSED.QUERY))
                        (DESTINATION (CREATE.FINGER.STREAM NEWINP)))
                       (BEGIN.FINGER.OUTPUT DESTINATION USER HOST)
                       (QUERY.FINGER.SERVER USER HOST (if (TYPENAMEP DESTINATION 'WINDOW)
                                                          then (TEXTSTREAM DESTINATION)
                                                        else DESTINATION))
                       (END.FINGER.OUTPUT DESTINATION))
           else (PRINTOUT T "Invalid query: " QUERY])

(QUERY.FINGER.SERVER
  [LAMBDA (USER HOST STREAM)                                 (* ; "Edited  3-Jan-2025 20:47 by PA")
                                                             (* ; "Edited  2-Jan-2025 17:28 by PA")
                                                             (* ; "Edited 28-Dec-2024 19:20 by PA")

         (* Queries Finger server HOST about USER and prints the response to STREAM which 
         defaults to the primary output if not supplied.
         If USER is NIL requests a list of users at HOST.)

    (ShellCommand (CONCAT "echo %"" (OR USER "")
                         "%"|nc -C -w 3 " HOST " finger|tr -d %"\r%"")
           (OR STREAM (OUTPUT])
)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (904 10061 (BEGIN.FINGER.OUTPUT 914 . 2318) (CREATE.FINGER.STREAM 2320 . 3772) (
END.FINGER.OUTPUT 3774 . 4767) (HANDLE.FINGER.MENU 4769 . 6910) (PARSE.FINGER.QUERY 6912 . 8234) (
PINKIE 8236 . 9336) (QUERY.FINGER.SERVER 9338 . 10059)))))
STOP
